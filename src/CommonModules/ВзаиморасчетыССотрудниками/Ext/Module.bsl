&ИзменениеИКонтроль("СоздатьВТЗарплатаКВыплатеОграниченнаяСальдоФизлиц")
Процедура Расш_СоздатьВТЗарплатаКВыплатеОграниченнаяСальдоФизлиц(МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры) Экспорт
	
	Если Параметры.ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс Тогда 
		ПериодВзаиморасчетов = ДобавитьМесяц(Параметры.ПериодРегистрации, -1);
	ИначеЕсли Параметры.ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата Тогда
		ПериодВзаиморасчетов = Параметры.ПериодРегистрации;
	Иначе
		Возврат
	КонецЕсли;	
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация",				Параметры.Организация);
	Запрос.УстановитьПараметр("ПериодРегистрации",			Параметры.ПериодРегистрации);
	Запрос.УстановитьПараметр("ПериодВзаиморасчетов",		КонецМесяца(ПериодВзаиморасчетов));
	Запрос.УстановитьПараметр("ИгнорируемыеРегистраторы",	Параметры.ИгнорируемыеРегистраторы);	
	
	// Зарплата по физлицам
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗарплатаСотрудников.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ЗарплатаСотрудников.КВыплате) КАК КВыплате
		|ПОМЕСТИТЬ ВТЗарплатаФизлиц
		|ИЗ
		|	ВТЗарплатаКВыплате КАК ЗарплатаСотрудников
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗарплатаСотрудников.ФизическоеЛицо";
	Запрос.Выполнить();	
	
	ПараметрыПолученияСотрудников =
		КадровыйУчет.ПараметрыПолученияСотрудниковОрганизацийПоВременнойТаблице();
	ПараметрыПолученияСотрудников.Организация			= Параметры.Организация;
	ПараметрыПолученияСотрудников.ИмяВТФизическиеЛица	= "ВТЗарплатаФизлиц";
	ПараметрыПолученияСотрудников.ОкончаниеПериода		= МИН(КонецМесяца(Параметры.ПериодРегистрации), Параметры.Дата);
	
	// Получаем всех сотрудников переданных физлиц
	КадровыйУчет.СоздатьВТСотрудникиОрганизации(
		МенеджерВременныхТаблиц, Истина, 
		ПараметрыПолученияСотрудников,
		"ВТСотрудникиФизическихЛиц");
		
	ВыплачиватьПлановыйАванс = УчетНДФЛ.ДатаЗакона263ФЗ() > Параметры.Дата;
	
	Если ВыплачиватьПлановыйАванс Тогда
		// Запрашиваем плановые авансы всех сотрудников рассматриваемых физлиц
		СоздатьВТПлановыйАванс(МенеджерВременныхТаблиц, ТолькоРазрешенные, Параметры, "ВТСотрудникиФизическихЛиц");		
	Иначе
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо,
		|	0 КАК СуммаКВыплате
		|ПОМЕСТИТЬ ВТПлановыйАванс";
		Запрос.Выполнить();
	КонецЕсли;
	
	// Определяем плановые авансы физических лиц
	ВТПлановыйАвансФизическихЛиц = 
	"ВЫБРАТЬ
	|	ПлановыйАванс.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ПлановыйАванс.СуммаКВыплате) КАК КВыплате
	|ПОМЕСТИТЬ ВТПлановыйАвансФизическихЛиц
	|ИЗ
	|	ВТПлановыйАванс КАК ПлановыйАванс
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыйАванс.ФизическоеЛицо";

	Если Параметры.ПорядокВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Аванс Тогда 
		
		#Удаление
		// Остатки начислений первой половины месяца
		ВТНачисленныйАвансФизическихЛиц = 
		"ВЫБРАТЬ
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ЗарплатаКВыплатеАвансом.СуммаКВыплате) КАК КВыплате
		|ПОМЕСТИТЬ ВТНачисленныйАвансФизическихЛиц
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
		|		ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплате
		|	ИЗ
		|		РегистрНакопления.ЗарплатаКВыплатеАвансом.Остатки(
		|				КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
		|				Организация = &Организация
		|					И ПериодВзаиморасчетов = &ПериодРегистрации
		|					И ФизическоеЛицо В
		|						(ВЫБРАТЬ
		|							ЗарплатаФизлиц.ФизическоеЛицо
		|						ИЗ
		|							ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеОстатки
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗарплатаКВыплате.ФизическоеЛицо,
		|		ВЫБОР
		|			КОГДА ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЗарплатаКВыплате.СуммаКВыплате
		|			ИНАЧЕ ЗарплатаКВыплате.СуммаКВыплате
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.ЗарплатаКВыплатеАвансом КАК ЗарплатаКВыплате
		|	ГДЕ
		|		ЗарплатаКВыплате.Регистратор В(&ИгнорируемыеРегистраторы)
		|		И ЗарплатаКВыплате.Организация = &Организация
		|		И ЗарплатаКВыплате.Период <= КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
		|		И ЗарплатаКВыплате.ПериодВзаиморасчетов = &ПериодРегистрации
		|		И ЗарплатаКВыплате.ФизическоеЛицо В
		|				(ВЫБРАТЬ
		|					ЗарплатаФизлиц.ФизическоеЛицо
		|				ИЗ
		|					ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеАвансом
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо";
		#КонецУдаления
		#Вставка
		// Остатки начислений первой половины месяца
		ВТНачисленныйАвансФизическихЛиц = 
        "ВЫБРАТЬ
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ЗарплатаКВыплатеАвансом.СуммаКВыплате) КАК КВыплате
		|ПОМЕСТИТЬ ВТНачисленныйАвансФизическихЛиц
		|ИЗ (ВЫБРАТЬ
		|		ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
		|		Сумма(Выбор Когда ЗарплатаКВыплатеОстатки.ВидДвижения = Значение(ВидДвиженияНакопления.Приход)
		|		И ТипЗначения(ЗарплатаКВыплатеОстатки.Регистратор) = ТипЗначения(ЗарплатаКВыплатеОстатки.ДокументОснование)
		|		Тогда ЗарплатаКВыплатеОстатки.СуммаКВыплате
		|		Когда ЗарплатаКВыплатеОстатки.ВидДвижения = Значение(ВидДвиженияНакопления.Расход)
		|		Тогда -1 * ЗарплатаКВыплатеОстатки.СуммаКВыплате
		|		Иначе 0
		|		Конец)  КАК СуммаКВыплате
		|	ИЗ
		|		РегистрНакопления.ЗарплатаКВыплатеАвансом Как ЗарплатаКВыплатеОстатки
		|	ГДЕ ЗарплатаКВыплатеОстатки.Период < ДобавитьКДате(НачалоПериода(&ПериодРегистрации, Месяц), Месяц, 1)
		|       			И Организация = &Организация
		|					И ПериодВзаиморасчетов = &ПериодРегистрации
		|					И ФизическоеЛицо В
		|						(ВЫБРАТЬ
		|							ЗарплатаФизлиц.ФизическоеЛицо
		|						ИЗ
		|							ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)
		|	Сгруппировать По
		|		ЗарплатаКВыплатеОстатки.ФизическоеЛицо
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЗарплатаКВыплате.ФизическоеЛицо,
		|		ВЫБОР
		|			КОГДА ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|				ТОГДА -ЗарплатаКВыплате.СуммаКВыплате
		|			ИНАЧЕ ЗарплатаКВыплате.СуммаКВыплате
		|		КОНЕЦ
		|	ИЗ
		|		РегистрНакопления.ЗарплатаКВыплатеАвансом КАК ЗарплатаКВыплате
		|	ГДЕ
		|		ЗарплатаКВыплате.Регистратор В(&ИгнорируемыеРегистраторы)
		|		И ЗарплатаКВыплате.Организация = &Организация
		|		И ЗарплатаКВыплате.Период <= КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
		|		И ЗарплатаКВыплате.ПериодВзаиморасчетов = &ПериодРегистрации
		|		И ЗарплатаКВыплате.ФизическоеЛицо В
		|				(ВЫБРАТЬ
		|					ЗарплатаФизлиц.ФизическоеЛицо
		|				ИЗ
		|					ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеАвансом
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо";		
		#КонецВставки
		
	Иначе 	
		
		// Начисленное в первой половине месяца 
		// (отрицательные итоги игнорируем ввиду их бессмысленности с точки зрения бизнес-логики).
		ВТНачисленныйАвансФизическихЛиц = 
		"ВЫБРАТЬ
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо КАК ФизическоеЛицо,
		|	СУММА(ЗарплатаКВыплатеАвансом.СуммаКВыплате) КАК КВыплате
		|ПОМЕСТИТЬ ВТНачисленныйАвансФизическихЛиц
		|ИЗ
		|	РегистрНакопления.ЗарплатаКВыплатеАвансом КАК ЗарплатаКВыплатеАвансом
		|ГДЕ
		|	ЗарплатаКВыплатеАвансом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|	И ЗарплатаКВыплатеАвансом.Организация = &Организация
		|	И ЗарплатаКВыплатеАвансом.Период <= КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
		|	И ЗарплатаКВыплатеАвансом.ПериодВзаиморасчетов = &ПериодРегистрации
		|	И ЗарплатаКВыплатеАвансом.ФизическоеЛицо В
		|			(ВЫБРАТЬ
		|				ЗарплатаФизлиц.ФизическоеЛицо
		|			ИЗ
		|				ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗарплатаКВыплатеАвансом.ФизическоеЛицо
		|
		|ИМЕЮЩИЕ
		|	СУММА(ЗарплатаКВыплатеАвансом.СуммаКВыплате) > 0";
		
	КонецЕсли;	
	
	#Удаление
	// Остатки зарплаты к выплате по физлицам
	ВТЗарплатаКВыплатеФизическихЛиц = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ЗарплатаКВыплатеОстатки.СуммаКВыплате) КАК КВыплате
	|ПОМЕСТИТЬ ВТЗарплатаКВыплатеФизическихЛиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплате
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплате.Остатки(
	|				,
	|				ПериодВзаиморасчетов <= &ПериодВзаиморасчетов
	|					И Организация = &Организация
	|					И ФизическоеЛицо В
	|						(ВЫБРАТЬ
	|							ЗарплатаФизлиц.ФизическоеЛицо
	|						ИЗ
	|							ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗарплатаКВыплате.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЗарплатаКВыплате.СуммаКВыплате
	|			ИНАЧЕ ЗарплатаКВыплате.СуммаКВыплате
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
	|	ГДЕ
	|		ЗарплатаКВыплате.Регистратор В(&ИгнорируемыеРегистраторы)
	|		И ЗарплатаКВыплате.ПериодВзаиморасчетов <= &ПериодВзаиморасчетов
	|		И ЗарплатаКВыплате.Организация = &Организация
	|		И ЗарплатаКВыплате.ФизическоеЛицо В
	|				(ВЫБРАТЬ
	|					ЗарплатаФизлиц.ФизическоеЛицо
	|				ИЗ
	|					ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо";
	#КонецУдаления
	#Вставка
	//Это удаление и вставка не нужны, но нужно смотреть, когда обновляешь, на весь текст процедуры, поэтому они оставлены
	// Остатки зарплаты к выплате по физлицам
	ВТЗарплатаКВыплатеФизическихЛиц = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ЗарплатаКВыплатеОстатки.СуммаКВыплате) КАК КВыплате
	|ПОМЕСТИТЬ ВТЗарплатаКВыплатеФизическихЛиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗарплатаКВыплатеОстатки.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ЗарплатаКВыплатеОстатки.СуммаКВыплатеОстаток КАК СуммаКВыплате
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплате.Остатки(
	|				,
	|				ПериодВзаиморасчетов <= &ПериодВзаиморасчетов
	|					И Организация = &Организация
	|					И ФизическоеЛицо В
	|						(ВЫБРАТЬ
	|							ЗарплатаФизлиц.ФизическоеЛицо
	|						ИЗ
	|							ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗарплатаКВыплате.ФизическоеЛицо,
	|		ВЫБОР
	|			КОГДА ЗарплатаКВыплате.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЗарплатаКВыплате.СуммаКВыплате
	|			ИНАЧЕ ЗарплатаКВыплате.СуммаКВыплате
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ЗарплатаКВыплате КАК ЗарплатаКВыплате
	|	ГДЕ
	|		ЗарплатаКВыплате.Регистратор В(&ИгнорируемыеРегистраторы)
	|		И ЗарплатаКВыплате.ПериодВзаиморасчетов <= &ПериодВзаиморасчетов
	|		И ЗарплатаКВыплате.Организация = &Организация
	|		И ЗарплатаКВыплате.ФизическоеЛицо В
	|				(ВЫБРАТЬ
	|					ЗарплатаФизлиц.ФизическоеЛицо
	|				ИЗ
	|					ВТЗарплатаФизлиц КАК ЗарплатаФизлиц)) КАК ЗарплатаКВыплатеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗарплатаКВыплатеОстатки.ФизическоеЛицо";
	#КонецВставки
		
	// Предел выплаты по физлицам
	ВТСальдоФизлиц = 
	"ВЫБРАТЬ
	|	ПределВыплат.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СУММА(ПределВыплат.КВыплате) КАК КВыплате
	|ПОМЕСТИТЬ ВТСальдоФизлиц
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПлановыйАванс.ФизическоеЛицо КАК ФизическоеЛицо,
	|		ПлановыйАванс.КВыплате КАК КВыплате
	|	ИЗ
	|		ВТПлановыйАвансФизическихЛиц КАК ПлановыйАванс
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НачисленныйАванс.ФизическоеЛицо,
	|		НачисленныйАванс.КВыплате
	|	ИЗ
	|		ВТНачисленныйАвансФизическихЛиц КАК НачисленныйАванс
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЗарплатаКВыплате.ФизическоеЛицо,
	|		ЗарплатаКВыплате.КВыплате
	|	ИЗ
	|		ВТЗарплатаКВыплатеФизическихЛиц КАК ЗарплатаКВыплате) КАК ПределВыплат
	|
	|СГРУППИРОВАТЬ ПО
	|	ПределВыплат.ФизическоеЛицо";
		
	// Коэффициент выплаты (отношение сальдо к зарплате, но не больше зарплаты)
	ВТКоэффициентыВыплаты = 
	"ВЫБРАТЬ
	|	Зарплата.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЕСТЬNULL(Сальдо.КВыплате, 0) КАК Сальдо,
	|	ВЫБОР
	|		КОГДА Зарплата.КВыплате <= 0
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(Сальдо.КВыплате, 0) / Зарплата.КВыплате > 1
	|			ТОГДА 1
	|		КОГДА ЕСТЬNULL(Сальдо.КВыплате, 0) < 0
	|			ТОГДА 0
	|		ИНАЧЕ (ВЫРАЗИТЬ(ЕСТЬNULL(Сальдо.КВыплате, 0) КАК ЧИСЛО(25, 10))) / Зарплата.КВыплате
	|	КОНЕЦ КАК Размер
	|ПОМЕСТИТЬ ВТКоэффициентыВыплаты
	|ИЗ
	|	ВТЗарплатаФизлиц КАК Зарплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСальдоФизлиц КАК Сальдо
	|		ПО Зарплата.ФизическоеЛицо = Сальдо.ФизическоеЛицо";
		
	// Ограничиваем суммы зарплаты коэффициентами выплаты
	ВТЗарплатаКВыплатеОграниченнаяСальдоФизлиц = 
	"ВЫБРАТЬ
	|	ЗарплатаКВыплате.Сотрудник КАК Сотрудник,
	|	ЗарплатаКВыплате.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗарплатаКВыплате.Подразделение КАК Подразделение,
	|	ЗарплатаКВыплате.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов,
	|	ЗарплатаКВыплате.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ЗарплатаКВыплате.СтатьяРасходов КАК СтатьяРасходов,
	|	ЗарплатаКВыплате.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	ЗарплатаКВыплате.ДокументОснование КАК ДокументОснование,
	|	ЗарплатаКВыплате.КВыплате * КоэффициентыВыплаты.Размер КАК КВыплате
	|ИЗ
	|	ВТЗарплатаКВыплате КАК ЗарплатаКВыплате
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТКоэффициентыВыплаты КАК КоэффициентыВыплаты
	|		ПО (КоэффициентыВыплаты.ФизическоеЛицо = ЗарплатаКВыплате.ФизическоеЛицо)";
	
	Запрос.Текст = ЗарплатаКадрыОбщиеНаборыДанных.ТекстОбъединенногоЗапроса(
		ВТПлановыйАвансФизическихЛиц,
		ВТНачисленныйАвансФизическихЛиц,
		ВТЗарплатаКВыплатеФизическихЛиц,
		ВТСальдоФизлиц,
		ВТКоэффициентыВыплаты,
		ВТЗарплатаКВыплатеОграниченнаяСальдоФизлиц);
		
	ВременнаяТаблицаКВыплате = Запрос.Выполнить().Выгрузить();
	ВременнаяТаблицаКВыплате.Индексы.Добавить("ФизическоеЛицо");
	
	ВременнаяТаблица = ВременнаяТаблицаКВыплате.СкопироватьКолонки();
	
	СтруктураОтбора = Новый Структура("ФизическоеЛицо");

	Запрос.Текст =
		"ВЫБРАТЬ
			|	Коэффициенты.ФизическоеЛицо КАК ФизическоеЛицо,
			|	Коэффициенты.Сальдо КАК Сальдо,
			|	Коэффициенты.Размер КАК Размер
			|ИЗ
			|	ВТКоэффициентыВыплаты КАК Коэффициенты";	
	Выборка = Запрос.Выполнить().Выбрать();
	ИмяКлюча = "Сотрудник,ФизическоеЛицо,Подразделение,ПериодВзаиморасчетов,СтатьяФинансирования,СтатьяРасходов,ВидДоходаИсполнительногоПроизводства,ДокументОснование";
	Пока Выборка.Следующий() Цикл
		СтруктураОтбора.ФизическоеЛицо = Выборка.ФизическоеЛицо;
		ВыплатаПоФизическомуЛицу = ВременнаяТаблицаКВыплате.Скопировать(СтруктураОтбора);
		Если Выборка.Размер > 0 И Выборка.Размер < 1 Тогда
			ЗарплатаКадры.ОкруглитьСлагаемыеПоСумме(ВыплатаПоФизическомуЛицу, "КВыплате", Выборка.Сальдо, 2, ИмяКлюча);
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВыплатаПоФизическомуЛицу, ВременнаяТаблица); 
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ВыплатаПоФизическомуЛицу, ВременнаяТаблица);
		КонецЕсли;
	КонецЦикла;
	
	// Удаляем временные таблицы
	УничтожаемыеВТ = Новый Массив;
	УничтожаемыеВТ.Добавить("ВТЗарплатаФизлиц");
	УничтожаемыеВТ.Добавить("ВТСотрудникиФизическихЛиц");
	УничтожаемыеВТ.Добавить("ВТПлановыйАванс");
	УничтожаемыеВТ.Добавить("ВТПлановыйАвансФизическихЛиц");
	УничтожаемыеВТ.Добавить("ВТНачисленныйАвансФизическихЛиц");
	УничтожаемыеВТ.Добавить("ВТЗарплатаКВыплатеФизическихЛиц");
	УничтожаемыеВТ.Добавить("ВТСальдоФизлиц");
	УничтожаемыеВТ.Добавить("ВТКоэффициентыВыплаты");
	ЗарплатаКадры.УничтожитьВТ(Запрос.МенеджерВременныхТаблиц, УничтожаемыеВТ); 
	
	// Перемещаем зарплату, ограниченную сальдо, в ВТЗарплатаКВыплате 
	Запрос.УстановитьПараметр("Таблица", ВременнаяТаблица);
	Запрос.Текст = 
	"УНИЧТОЖИТЬ ВТЗарплатаКВыплате
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗарплатаКВыплате.Сотрудник КАК Сотрудник,
	|	ЗарплатаКВыплате.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ЗарплатаКВыплате.Подразделение КАК Подразделение,
	|	ЗарплатаКВыплате.ПериодВзаиморасчетов КАК ПериодВзаиморасчетов,
	|	ЗарплатаКВыплате.СтатьяФинансирования КАК СтатьяФинансирования,
	|	ЗарплатаКВыплате.СтатьяРасходов КАК СтатьяРасходов,
	|	ЗарплатаКВыплате.ВидДоходаИсполнительногоПроизводства КАК ВидДоходаИсполнительногоПроизводства,
	|	ЗарплатаКВыплате.ДокументОснование КАК ДокументОснование,
	|	ЗарплатаКВыплате.КВыплате КАК КВыплате
	|ПОМЕСТИТЬ ВТЗарплатаКВыплате
	|ИЗ
	|	&Таблица КАК ЗарплатаКВыплате";
	
	Запрос.Выполнить();
	
	ВзаиморасчетыССотрудникамиВнутренний.ПодставитьНулевыеДокументыОснования(МенеджерВременныхТаблиц, Параметры);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

